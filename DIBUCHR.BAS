10 ' CONVIERTE GRAFICOS EN SCREEN 2
20 '
30 '   A CARACTERES EN SCREEN 1
40 '
50 '
70 COLOR 14,1,1:KEY OFF:SCREEN 0
80 '
90 INPUT"NOMBRE DE LA PANTALLA A CARGAR       ";NO$
100 INPUT"X (ESQUINA SUPERIOR IZQUIERDA)       ";X
110 INPUT"Y (ESQUINA SUPERIOR IZQUIERDA)       ";Y
120 INPUT"ANCHO DE X";AX
130 INPUT"ANCHO DE Y";AY
140 INPUT"PRIMER CARACTER";CA
150 INPUT"NOMBRE DEL PROGRAMA                  ";NP$
160 INPUT"PRIMER NUMERO DE LINEA";NL
170 SCREEN 2
180 BLOAD NO$,S
181 IF INKEY$<>"" THEN 181
182 IF INKEY$="" THEN 182
190 OPEN"A:"+NP$ FOR OUTPUT AS #1
200 PRINT#1,STR$(NL);" READ CA:FOR T=1 TO ";STR$(AX*AY)
210 PRINT#1,STR$(NL+10);" FOR I=0 TO 7:READ A:VPOKE BASE(1)+CA*8+I,A:NEXT I"
220 PRINT#1,STR$(NL+20);" CA=CA+1:NEXT T"
230 PRINT#1,STR$(NL+30);" DATA ";STR$(CA)
240 IN=40
250 '
260 DI=CA*8
270 FOR F=Y TO Y-1+AY
280 FOR G=X TO X-1+AX
290 PRINT#1,STR$(NL+IN);" DATA ";
300 FOR H=0 TO 7
310 A=VPEEK(F*256+H+G*8)
320 PRINT#1,RIGHT$(STR$(A),LEN(STR$(A))-1);
330 IF H<>7 THEN PRINT#1,",";ELSE PRINT#1," "
340 NEXT H
350 IN=IN+10:NEXT G
360 NEXT F
370 CLOSE
380 END
